
# Maintainer: Christian Menard <chrism333@gmx.de>
# Contributor: iruel <cockmomgler at gmail dot org>
# Contributor: tobias <tobias@archlinux.org>

_pkgbase=gnome-sharp
pkgname=('art-sharp')
pkgver=2.24.2
pkgrel=4
pkgdesc="Mono bindings for libart"
arch=(i686 x86_64)
license=(LGPL)
url="http://gtk-sharp.sourceforge.net"
depends=('gtk-sharp-2' 'libart-lgpl')
makedepends=('gtk-sharp-2' 'libgnomeui' 'monodoc')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/gnome-sharp/2.24/${_pkgbase}-${pkgver}.tar.bz2)
md5sums=('3b38f53960c736d4afb8f04204efe98b')

build() {
  # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR="${srcdir}/wapit"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make -C art install DESTDIR="${pkgdir}"
}