# Maintainer: Christian Menard <chrism333@gmx.de>
# Contributor: Zie Evolution <zie.evolutiion@googlemail.com>
# Contributor: tobias <tobias@archlinux.org>

pkgname=gtk-sharp-2
pkgver=2.12.11
pkgrel=4
pkgdesc="gtk2 bindings for C-sharp"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk-sharp.sourceforge.net"
depends=('mono>=2.4' 'libglade>=2.6.4' 'gtk2>=2.18.0')
makedepends=('pkgconfig' 'monodoc')
conflicts=('gtk#')
options=('!libtool' '!makeflags')
source=(http://download.mono-project.com/sources/gtk-sharp212/gtk-sharp-${pkgver}.tar.bz2 
glue-glib_fix.patch)
md5sums=('c278da6a472c6c13b698af012f543c64' 'f7e8b5ce81249775e21610b243440862')

build() {
 # get rid of that .wapi errors; thanks to brice
  
  export MONO_SHARED_DIR="${srcdir}/wapi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/gtk-sharp-${pkgver}/glib/glue"
  patch -uNp1 -i "${srcdir}/glue-glib_fix.patch"
  
  cd "${srcdir}/gtk-sharp-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/gtk-sharp-${pkgver}"
  make DESTDIR="${pkgdir}" install
}