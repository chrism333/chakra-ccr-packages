pkgname=openocd
pkgver=0.6.1
pkgrel=1
pkgdesc="Debugging, in-system programming and boundary-scan testing for embedded target devices"
arch=('i686' 'x86_64')
url="http://openocd.berlios.de"
license=('GPL')
depends=('libftdi')
options=(!strip !libtool)
install=openocd.install
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('946421efc2414ff89bdaf3f588b230f8')

# what features should be enabled on build
# 'ecos','zy1000' seams not to be supported on linux

_features=(parport ft2232_libftdi amtjtagaccel ep93xxat91rm9200gw16012 presto_libftdi usbprog oocd_trace jlink vsllink rlink arm-jtag-ew buspirate stlink)

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr ${_features[@]/#/--enable-} --disable-werror
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}