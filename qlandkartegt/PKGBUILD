# $Id$
# Maintainer: Christian Menard <chrism333@gmx.de>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: <boenki@gmx.de>

pkgname=qlandkartegt
pkgver=1.7.3
pkgrel=1
pkgdesc="Use your GPS with Linux"
arch=('i686' 'x86_64')
url="http://www.qlandkarte.org/"
license=('GPL')
depends=('gdal' 'gpsd' 'libdmtx' 'libexif' 'libmariadbclient' 'glu' 'postgresql-libs'
         'shared-mime-info' 'qtwebkit')
makedepends=('cmake' 'garmindev' 'mesa')
optdepends=('garmindev: to connect garmin devices')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname-mimetypes.xml)
sha256sums=('dbfc9636f4a8cc88737dff8cbd3b411e4b1ac17c552516396b55d1e680ad953c'
            'e8d33948831f8a66edd752c71c653085d6c4d9f1969e70dd8c40b9c7ca37fba0')

build() {
  cd "${srcdir}"
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package(){
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/$pkgname-mimetypes.xml" \
    "${pkgdir}/usr/share/mime/packages/$pkgname-mimetypes.xml"
}
 
